dependencies {
	compile			project(':syncany-util')
	
	compile			"commons-io:commons-io:2.4"	
	compile			"org.bouncycastle:bcprov-ext-jdk15on:1.49"            
	compile			"org.simpleframework:simple-xml:2.7.1"
	compile			"com.google.guava:guava:15.0"
	compile			"commons-codec:commons-codec:1.8"            
	compile			"org.reflections:reflections:0.9.8"
	compile			"org.slf4j:slf4j-api:1.6.0" // for reflections
	
	compile			"org.hsqldb:hsqldb:2.3.1"
	
	testCompile		project(path: ':syncany-util', configuration: 'tests')
	testCompile		"junit:junit:4.3"
}

def getRevision = { ->
    def stdout = new ByteArrayOutputStream()
    
    exec {
	workingDir rootProject.projectDir
        commandLine 'git rev-parse --short HEAD'.split()
        standardOutput = stdout
    }
    
    return stdout.toString().trim()
}

processResources {
	String applicationRevision = getRevision()
	Date compileDate = new Date()
	
	filter{ String line -> line.replaceAll('%RELEASE%', "${applicationRelease}") }        	            
	filter{ String line -> line.replaceAll('%VERSION%', "${applicationVersion}") }        	            
	filter{ String line -> line.replaceAll('%REVISION%', "${applicationRevision}") }
	filter{ String line -> line.replaceAll('%COMPILEDATE%', "${compileDate}") }          	            
}
	
